#include "earth_params.h"
#include "../stdlib_vp.h"

#define varTSView gl_TexCoord[1]
#define varTSLight gl_TexCoord[2]
#define varWSNormal gl_TexCoord[3]

void main()
{
   // Compute position, eye-to-object direction and normalized world-space normal
   vec4 extrudedPosition = gl_Vertex + vec4(normalize(gl_Normal.xyz),0) * fAtmosphereExtrusion.x;
   vec3 normal   = normalize(gl_NormalMatrix * gl_Normal);
   vec3 tangent  = normalize(gl_NormalMatrix * gl_MultiTexCoord2.xyz);
   vec3 binormal = cross(tangent, normal) * sign(gl_MultiTexCoord2.w);
   
   // Extrude sphere
   vec4 position = gl_ModelViewMatrix * extrudedPosition;
   vec3 eyetopos = normalize(position.xyz);
   
   vec4 lightpos;
   float lightatt;
   lightPosSizeAndAtt0(position, lightpos, lightatt);
   
   // TS view vector
   varTSView.x = dot( tangent, -eyetopos );
   varTSView.y = dot( binormal, -eyetopos );
   varTSView.z = dot( normal, -eyetopos );
   
   // TS light posision
   varTSLight.x = dot( tangent, lightpos.xyz );
   varTSLight.y = dot( binormal, lightpos.xyz );
   varTSLight.z = dot( normal, lightpos.xyz );
   
   // WS normal
   varWSNormal.xyz = normal;
   
   // Load varyings
   gl_TexCoord[0] = gl_MultiTexCoord0;
   
   // set primary color to the emissive material properties
   vec4 pc = gl_FrontMaterial.emission;
   vec4 sc = vec4(0.0);
   pc.a = gl_FrontMaterial.diffuse.a;
   
   pc.rgb += gl_FrontMaterial.diffuse.rgb * gl_LightSource[0].diffuse.rgb * lightatt;
   
   // Need this instead of ftransform() for invariance
   gl_Position = gl_ModelViewProjectionMatrix * extrudedPosition;
   gl_FrontColor = gl_BackColor = pc;
   gl_FrontSecondaryColor = gl_BackSecondaryColor = sc;
}


